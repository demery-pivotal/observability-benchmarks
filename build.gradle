plugins {
    id 'java'
    id 'me.champeau.gradle.jmh' version '0.4.7'
}

group 'org.apache.geode'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile group: 'io.micrometer', name: 'micrometer-core', version: 'latest.release'
    compile group: 'org.apache.geode', name: 'geode-core', version: 'latest.release'
    compile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: 'latest.release'
    compile group: 'org.openjdk.jmh', name: 'jmh-core', version: 'latest.release'
    compile group: 'org.springframework.shell', name: 'spring-shell', version: '1.2.0.RELEASE'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.6'
}

compileJava {
    options.compilerArgs += '-proc:none'
}

// Copy these project properties into jmh as strings
['timeOnIteration'].each {
    if (project.hasProperty(it)) {
        jmh.setProperty(it, project[it])
    }
}

// Copy these project properties into jmh as integers
['iterations', 'warmupIterations'].each {
    if (project.hasProperty(it)) {
        jmh.setProperty(it, project[it] as Integer)
    }
}

jmh {
    // This may not be needed for Gradle 5+
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
}

wrapper {
    gradleVersion = 4.10
}
